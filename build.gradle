buildscript {
    ext {
        springBootVersion = '1.3.5.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        // Spring Boot plugin
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'spring-boot'
    apply plugin: 'eclipse'
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"

    archivesBaseName = 'spring-cloud-demo'
    group = 'com.curious'
    version = '1.0'

    // Global settings
    ext {
        // dependencies library versions
        springCloudVersion = '1.2.1.RELEASE'
        springCloudAwsVersion = '1.1.3.RELEASE'
        commonsIoVersion = '2.5'
    }

    repositories {
        mavenLocal()
        maven {url 'http://maven.aliyun.com/nexus/content/groups/public/'}
        mavenCentral()
        jcenter()
    }

    // Settings for the compile task
    tasks.withType(JavaCompile) {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        options.encoding = 'UTF-8'
        options.debug = true
        options.compilerArgs << "-Xlint:unchecked"
        options.compilerArgs << "-Xlint:deprecation"
    }

    // Apply 'war' plugin for all war projects
    configure([project(":cloud-config-server"),
               project(":cloud-simple-server-a"),
               project(":cloud-media-service"),
               project(":cloud-eureka-server"),
               project(":cloud-compute-service")]) {
        apply plugin: 'war'
    }

    task wrapper(type: Wrapper) {
        gradleVersion = '2.9'
    }

    configurations {
        compile.exclude group:'org.apache.logging.log4j'
    }

}

project(':cloud-eureka-server') {

    war {
        classifier = 'cloud-eureka-server'
    }

    dependencies {
        // Spring Boot MVC
        compile("org.springframework.boot:spring-boot-starter-web")
        // Spring cloud eureka server
        compile("org.springframework.cloud:spring-cloud-starter-eureka-server:${springCloudVersion}")
    }
}

project(':cloud-config-server') {

    war {
        classifier = 'cloud-config-server'
    }

    dependencies {
        // Spring cloud config server
        compile("org.springframework.cloud:spring-cloud-config-server:${springCloudVersion}")
        // Spring Boot actuator
        compile("org.springframework.boot:spring-boot-starter-actuator")
        // Spring cloud bus
//        compile("org.springframework.cloud:spring-cloud-starter-bus-amqp:${springCloudVersion}")
        // Spring cloud eureka
        compile("org.springframework.cloud:spring-cloud-starter-eureka:${springCloudVersion}")

    }
}

project(':cloud-simple-server-a') {
    war {
        classifier = 'cloud-simple-server-a'
    }

    dependencies {
        // Spring Boot MVC
        compile("org.springframework.boot:spring-boot-starter-web")
        // Spring Boot actuator
        compile("org.springframework.boot:spring-boot-starter-actuator")
        // Spring cloud config
        compile("org.springframework.cloud:spring-cloud-starter-config:${springCloudVersion}")
        // Spring cloud config server
        compile("org.springframework.cloud:spring-cloud-config-client:${springCloudVersion}")
        // Spring cloud bus
//        compile("org.springframework.cloud:spring-cloud-starter-bus-amqp:${springCloudVersion}")
    }
}

project(':cloud-media-service') {
    war {
        classifier = 'cloud-media-service'
    }

    dependencies {
        // Spring Boot MVC
        compile("org.springframework.boot:spring-boot-starter-web")
        // Spring Boot actuator
        compile("org.springframework.boot:spring-boot-starter-actuator")
        // Spring cloud config
        compile("org.springframework.cloud:spring-cloud-starter-config:${springCloudVersion}")
        // Spring cloud config server
        compile("org.springframework.cloud:spring-cloud-config-client:${springCloudVersion}")
        // Spring cloud bus
//        compile("org.springframework.cloud:spring-cloud-starter-bus-amqp:${springCloudVersion}")
        // Spring Boot JPA
        compile("org.springframework.boot:spring-boot-starter-data-jpa")
        // MySQL Java Driver
        compile("mysql:mysql-connector-java")

    }
}

project(':cloud-compute-service') {

    war {
        classifier = 'cloud-compute-service'
    }

    dependencies {
        // Spring cloud eureka
        compile("org.springframework.cloud:spring-cloud-starter-eureka:${springCloudVersion}")
        // Spring Boot MVC
        compile("org.springframework.boot:spring-boot-starter-web")
        // Spring Boot actuator
        compile("org.springframework.boot:spring-boot-starter-actuator")
    }
}